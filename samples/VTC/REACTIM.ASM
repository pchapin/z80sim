USER	EQU	00H
PORTA	EQU	0C0H		; 8255A definitions.
PORTB	EQU	0C1H
CNTPRT	EQU	0C3H
CTC0	EQU	040H		; Z-80 CTC definitions.
CTC1	EQU	041H
DELAY	EQU	0FFDH		; CAMI subroutines.
;
	ORG	1800H
; ***********************************************************
START:
;	The following program is a solution to Lab #11
;	              "Reaction Timer"
; ***********************************************************
	LD	A,10000010B	; Configure 8255A.
	OUT	(CNTPRT),A
	LD	A,0FFH		; Tell user all is ready.
	OUT	(PORTA),A
;
NEXT:	CALL	RANDOM		; Wait for user key, etc.
;
	LD	A,00H		; Clear display.
	OUT	(PORTA),A
	LD	A,00100101B	; Channel 0 = timer.
	OUT	(CTC0),A
	LD	A,00H		; TC = 256.
	OUT	(CTC0),A
	LD	A,01000101B	; Channel 1 = counter.
	OUT	(CTC1),A
	LD	A,00H		; TC = 256.
	OUT	(CTC1),A
;
AGAIN:	IN	A,(PORTB)	; Wait for button press.
	BIT	0,A
	JP	Z,AGAIN
;
	IN	A,(CTC1)	; How many timeouts?
	NEG			; Take two's complement.
	OUT	(PORTA),A	; Display.
	JP	NEXT
;
; ***********************************************************
RANDOM:
;	This routine waits until the USER key is pressed and
;	then delays for a random interval in the range of 3
;	to 7 seconds. No registers are changed.
; ***********************************************************
	PUSH	AF		; Save registers.
	PUSH	BC
	PUSH	DE
	PUSH	HL
;
	; A random number is generated by incrementing a reg-
	; ister as fast as possible. By the time the user has
	; pressed the USER key, the register will have "wrapped"
	; around several times and its contents will be random.
;
TRYAGN:	IN	A,(USER)	; Is USER key pressed?
	BIT	6,A
	JP	Z,DODLY		; Yes... do random delay.
	INC	E		; Inc random number.
	JP	TRYAGN
;
DODLY:	LD	D,00H		; Make DE = 16 bit version of E.
	LD	B,4		; Multiply DE by 2^4.
SHIFT:	SLA	E
	RL	D
	DJNZ	SHIFT
	LD	HL,3000		; Delay no less than 3 Secs.
	ADD	HL,DE		; As long as 3+16*(.256) Secs.
	CALL	DELAY
;
	POP	HL		; Restore registers.
	POP	DE
	POP	BC
	POP	AF
	RET
;
	END
